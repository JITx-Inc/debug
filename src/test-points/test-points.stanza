#use-added-syntax(jitx)
defpackage debug/test-points/test-points :
  import core

  import jitx
  import jitx/commands

  import jsl/errors

  import debug/test-points/symbol
  import debug/test-points/landpattern

val ID-COUNTER = to-seq(0 to false)

doc: \<DOC>
Automatically generates test points for a given object.
Given a net or a port, instantiate a test point for each
single-pin net or port contained within the JITX object.
Returns a tuple of Instances representing all new test points.

@param obj: Either a port or a net
@param tp: An instantiable for the test points
@param reference-prefix: A prefix for the test point reference designators (default: "TP")

In this example, JITX will instantiate test points for
both the positive and negative leads of the power net, given
a custom `MyTestPoint` instantiable. 
```stanza
pcb-module my-module :
  port pwr : power
  inst my-inst : my-component 
  net PWR (self.pwr, my-inst.pwr)
  make-test-points(PWR, MyTestPoint)
```
<DOC>
public defn make-test-points (
    obj:JITXObject,
    tp:Instantiable
    -- reference-prefix:String = "TP")
    -> Tuple<Instance> :
  make-test-point-per-pin(obj, tp, false, reference-prefix)

doc: \<DOC>
Automatically generates test points for a given object.
Given a net or a port, instantiate a test point for each
single-pin net or port contained within the JITX object.
Returns a tuple of Instances representing all new test points.

@param obj: Either a port or a net
@param shape: The shape of the test point 
@param symbol-params: Parameters describing the test points' schematic symbols 
@param reference-prefix: Prefix of the test points' reference designators (default: "TP")

In this example, JITX will instantiate test points for
both the positive and negative leads of the power net. Each 
test points will have a small circular SMD pad.
```stanza
pcb-module my-module :
  port pwr : power
  inst my-inst : my-component 
  net PWR (self.pwr, my-inst.pwr)
  make-test-points(PWR, Circle(0.5))
```
<DOC>
public defn make-test-points (
    obj:JITXObject,
    shape:Shape = Circle(1.0)
    symbol-params:TestPointSymbolParams = TestPointSymbolParams(),
    -- reference-prefix:String = "TP")
    -> Tuple<Instance> :
  make-test-point-per-pin(obj, TestPoint(shape, symbol-params), false, reference-prefix)

; Traverse port/net structure, attach one test point to each
; single-pin port or net
defn make-test-point-per-pin (
    target:JITXObject,
    point:Instantiable,
    name:String|False,
    reference-prefix:String)
    -> Tuple<Instance> :
  match(target) :
    (obj:Pin) :
      to-tuple $ for p in pins(obj) seq : instantiate-test-point(p, point, name, reference-prefix)
    (obj:Net) :
      match(port-type(obj)) :
        (t:SinglePin) : [instantiate-test-point(obj, point, name, reference-prefix)]
        (otherwise) :
          to-tuple $ for n in single-pin-nets(obj) seq : instantiate-test-point(n, point, name, reference-prefix)
    (obj) : throw(ArgumentError("Can only call make-test-points with a pin or net; given %_" % [obj]))

defn instantiate-test-point (pin:JITXObject, component:Instantiable, ref:String|False, prefix) -> Instance :
  inside pcb-module :
    inst tp : component
    net (tp.p, pin)
    ; TODO : custom names
    match(ref:String) :
      reference-designator(tp) = ref
    else :
      reference-designator(tp) = to-string("%_%_" % [prefix, next(ID-COUNTER)])
    tp

; Parameterizable instantiable
defn TestPoint (shape:Shape, symbol-params:TestPointSymbolParams) -> Instantiable :
  pcb-component comp :

    pin-properties:
      [pin:Ref | pads:Ref ...]
      [p       | p           ]

    ; Schematic symbol
    assign-symbol(testpoint-sym(symbol-params))

    ; Assign landpattern
    assign-landpattern(testpoint-pkg(shape))
  comp
