#use-added-syntax(jitx)
defpackage debug/tag-connect/TC2050-IDC :
  import core

  import jitx
  import jitx/commands

  import jsl/symbols
  import jsl/landpatterns/pads
  import jsl/landpatterns

pcb-landpattern TC2050-IDC-NL-footprint :
  for (l in grid-locs(2, 5, 1.27, 1.27), i in [10 9 8 7 6 1 2 3 4 5]) do :
    pad p[i] : testpoint-pad(0.787) at l
  layer(Cutout()) = Circle((- 3.81), 0.0, 0.5)
  layer(Cutout()) = Circle(3.81, 1.015, 0.5)
  layer(Cutout()) = Circle(3.81, (- 1.015), 0.5)
  layer(Courtyard(Top)) = Rectangle(8.62, 3.03)
  ref-label()

pcb-landpattern TC2050-IDC-footprint :
  for (l in grid-locs(2, 5, 1.27, 1.27), i in [10 9 8 7 6 1 2 3 4 5]) do :
    pad p[i] : testpoint-pad(0.787) at l
  layer(Cutout()) = Circle((- 3.81), 0.0, 0.5)
  layer(Cutout()) = Circle(3.81, 1.015, 0.5)
  layer(Cutout()) = Circle(3.81, (- 1.015), 0.5)

; Leg Latch Holes  
  layer(Cutout()) = Circle((- 3.81), 2.54, 1.1875)
  layer(Cutout()) = Circle((- 3.81), -2.54, 1.1875)
  layer(Cutout()) = Circle(1.905, ( 2.54), 1.1875)
  layer(Cutout()) = Circle(1.905, (- 2.54), 1.1875)
  layer(Courtyard(Top)) = Rectangle(10.16, 7.62)
  ref-label()

; [TODO] : support NL option
public pcb-component component (no-legs?:True|False = false) :
  description = "Insertion point for TC-2050 connector"
  datasheet = "https://www.tag-connect.com/wp-content/uploads/bsk-pdf-manager/TC2050-IDC_Datasheet_7.pdf"
  manufacturer = "Tag-Connect"
  mpn = "TC2050-IDC"
  port p: pin[1 through 10]
  pin-properties :
    [pin:Ref     | pads:Int ... | side:Dir]
    [p[1]        | 1            | Left    ]
    [p[2]        | 2            | Left    ]
    [p[3]        | 3            | Left    ]
    [p[4]        | 4            | Left    ]
    [p[5]        | 5            | Left    ]
    [p[10]       | 10           | Right   ]
    [p[9]        | 9            | Right   ]
    [p[8]        | 8            | Right   ]
    [p[7]        | 7            | Right   ]
    [p[6]        | 6            | Right   ]

  if no-legs? :
    assign-landpattern(TC2050-IDC-NL-footprint)
  else :
    assign-landpattern(TC2050-IDC-footprint)

  reference-prefix = "J"
  val box = BoxSymbol(self)
  val sym = create-symbol(box)
  assign-symbol(sym)
  ;make-box-symbol()
